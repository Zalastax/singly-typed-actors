\begin{code}
module Examples.SizedStream where
open import Data.Nat
open import Relation.Binary.PropositionalEquality using (_≡_ ; refl)
open import Size using (Size ; Size<_ ; ↑_)

\end{code}
%<*Stream>
\begin{code}
record Stream (i : Size) (A : Set) : Set where
  coinductive
  field
    head : A
    tail : ∀ {j : Size< i} → Stream j A
\end{code}
%</Stream>
\begin{code}
open Stream
\end{code}
%<*SkipTwice>
\begin{code}
skip-twice : {i : Size} → Stream (↑ (↑ i)) ℕ → Stream i ℕ
skip-twice s = s .tail .tail
\end{code}
%</SkipTwice>
%<*Fib>
\begin{code}
zipWith : {i : Size} {A B C : Set} → (A → B → C) →
          Stream i A → Stream i B → Stream i C
(zipWith f xs ys) .head = f (xs .head) (ys .head)
(zipWith f xs ys) .tail = zipWith f (xs .tail) (ys .tail)

fib : {i : Size} → Stream i ℕ
fib .head = 0
fib .tail .head = 1
fib .tail .tail = zipWith _+_ fib (fib .tail)
\end{code}
%</Fib>
