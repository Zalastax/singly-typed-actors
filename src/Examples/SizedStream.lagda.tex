\begin{code}
module Examples.SizedStream where
open import Prelude

\end{code}
%<*Stream>
\begin{code}
record Stream (i : Size) (A : Set) : Set where
  coinductive
  field
    head : A
    tail : (j : Size< i) → Stream j A
\end{code}
%</Stream>
\begin{code}
open Stream
\end{code}
%<*SkipTwice>
\begin{code}
skip-twice : (i : Size) → Stream (↑ (↑ i)) ℕ → Stream i ℕ
skip-twice i s = s .tail (↑ i) .tail i
\end{code}
%</SkipTwice>
%<*Fib>
\begin{minipage}{\textwidth}
\begin{code}
zipWith : (i : Size) {A B C : Set} → (A → B → C) →
          Stream i A → Stream i B → Stream i C
(zipWith i f xs ys) .head = f (xs .head) (ys .head)
(zipWith i f xs ys) .tail j = zipWith j f (xs .tail j) (ys .tail j)
\end{code}
\end{minipage}

\begin{minipage}{\textwidth}
\begin{code}
fib : (i : Size) → Stream i ℕ
fib i .head = 0
fib i .tail j .head = 1
fib i .tail j .tail k = zipWith k _+_ (fib k) (fib j .tail k)
\end{code}
\end{minipage}
%</Fib>
