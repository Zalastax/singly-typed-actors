\begin{code}
module Examples.Call where

open import ActorMonad public
open import Prelude
open import Examples.SelectiveReceive using (
            selective-receive ; SelRec ; waiting-refs
            ; add-references++)
open SelRec

UniqueTag = ℕ

call-select-unwrap : ∀ {MtIn MT} {IS : InboxShape} →
                     UniqueTag →
                     MT ∈ IS →
                     All receive-field-content MT →
                     (ValueType UniqueTag ∷ MtIn) ∈ IS →
                     Bool
call-select-unwrap tag Z (tag' ∷ fs) Z with (tag ≟ tag')
... | yes p = true
... | no p = false
call-select-unwrap _ Z _ (S p) = false
call-select-unwrap _ (S x) _ Z = false
call-select-unwrap tag (S x) fs (S p) = call-select-unwrap tag x fs p

call-select : ∀ {IS IS' MtIn} →
              UniqueTag →
              IS' <: IS →
              (ValueType UniqueTag ∷ MtIn) ∈ IS' →
              Message IS →
              Bool
call-select tag sub which (Msg x fs) =
  call-select-unwrap tag x fs (translate-⊆  sub which)

call : ∀ {Γ MtTo MtIn i} {To IS IS' : InboxShape} →
       (q : List (Message IS)) →
       (Γ ⊢ To) →
       ((ValueType UniqueTag ∷ ReferenceType IS' ∷ MtTo) ∈ To) →
       (tag : UniqueTag) →
       All (send-field-content Γ) MtTo →
       (ISsubs : IS' <: IS) →
       (whichIn : (ValueType UniqueTag ∷ MtIn) ∈ IS') →
       ∞ActorM i IS
         (SelRec IS (call-select tag ISsubs whichIn))
         (Γ ++ (waiting-refs q))
         (λ m → (add-references Γ (msg m)) ++ (waiting-refs (waiting m)))
call {Γ} {IS = IS} q var toFi tag vals sub whichIn = do
     self
     S (translate-⊆ (⊆++-refl Γ (waiting-refs q)) var)
       ![t: toFi ]
       ((lift tag) ∷ ([ Z ]>: sub) ∷ (translate-fields vals))
     strengthen (⊆-suc ⊆-refl)
     selective-receive q (call-select tag sub whichIn)
  where
    translate-fields : ∀ {MT} → All (send-field-content  Γ) MT →
                         All (send-field-content (IS ∷ Γ ++ waiting-refs q)) MT
    translate-fields {[]} [] = []
    translate-fields {ValueType x ∷ MT} (px ∷ ps) =
      px ∷ translate-fields ps
    translate-fields {ReferenceType x ∷ MT} (([ p ]>: v) ∷ ps) =
      ([ (S (∈-inc Γ (waiting-refs q) _ p)) ]>: v) ∷ (translate-fields ps)

AddReplyMessage : MessageType
AddReplyMessage = ValueType UniqueTag ∷ [ ValueType ℕ ]ˡ

AddReply : InboxShape
AddReply = [ AddReplyMessage ]ˡ

AddMessage : MessageType
AddMessage = ValueType UniqueTag ∷ ReferenceType AddReply ∷ ValueType ℕ ∷ [ ValueType ℕ ]ˡ

Calculator : InboxShape
Calculator = [ AddMessage ]ˡ

calculatorActor : ∀ {i} → ∞ActorM (↑ i) Calculator (Lift ⊤) [] (λ _ → [])
calculatorActor = loop
  where
    loop : ∀ {i} → ∞ActorM i Calculator (Lift ⊤) [] (λ _ → [])
    loop .force = receive ∞>>= λ {
      (Msg Z (tag ∷ _ ∷ n ∷ m ∷ [])) → do
        Z ![t: Z ] ((lift tag) ∷ [ lift (n + m) ]ᵃ )
        strengthen []
        loop
      ; (Msg (S ()) _) }

TestBox : InboxShape
TestBox = AddReply

calltestActor : ∀ {i} → ∞ActorM i TestBox (Lift ℕ) [] (λ _ → [])
calltestActor .force = spawn∞ calculatorActor ∞>> do
    x ← call [] Z Z 0
         ((lift 10) ∷ [ lift 32 ]ᵃ)
         ⊆-refl Z
    strengthen []
    return-result x
  where
    return-result : SelRec TestBox (call-select 0 ⊆-refl Z) →
                    ∀ {i} → ∞ActorM i TestBox (Lift ℕ) [] (λ _ → [])
    return-result record { msg = (Msg Z (tag ∷ n ∷ [])) } = return n
    return-result record { msg = (Msg (S x) x₁) ; msg-ok = () }

\end{code}
