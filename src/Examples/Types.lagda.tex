\begin{code}
module Examples.Types where

open import Level using (_⊔_)

open import Data.Unit using (⊤ ; tt)
open import Data.Bool using (Bool ; false ; true)
open import Data.Nat using (ℕ ; zero ; suc ; _+_)
open import Data.Vec using (Vec ; [] ;  _∷_)
open import Data.List using (List ; [] ; _∷_)
open import Data.List.All using (All ; [] ; _∷_)
open import Data.String using (String)
open import ActorMonad
open import Membership using (_∈_; _⊆_ ; _∷_ ; [] ; S ; Z)

record Singleton {a b} (A : Set a) (B : Set b) : Set (a ⊔ b) where
  field
    [_] : A → B
open Singleton {{...}}

instance
  singletonList : ∀{a}{A : Set a} → Singleton A (List A)
  singletonList .Singleton.[_] a = a ∷ []

  singletonAll  : ∀{a p} {A : Set a} {P : A → Set p} {x : A} → Singleton (P x) (All P (x ∷ []))
  singletonAll .Singleton.[_] p = p ∷ []

  singletonMem :  ∀{a} {A : Set a} {x : A} {ys : List A} → Singleton (x ∈ ys) ((x ∷ []) ⊆ ys)
  singletonMem .Singleton.[_] p = p ∷ []

[_]ˡ : ∀{a}{A : Set a} → A → List A
[ a ]ˡ = a ∷ []

[_]ᵐ :  ∀{a} {A : Set a} {x : A} {ys : List A} → (x ∈ ys) → ((x ∷ []) ⊆ ys)
[ p ]ᵐ = p ∷ []
\end{code}

%<*PropositionalEquality>
\begin{code}
data _≡_ {a} {A : Set a} (x : A) : A → Set a where
  refl : x ≡ x

trivial : 4 ≡ 4
trivial = refl
definitional : (2 + 2) ≡ 4
definitional = refl
\end{code}
%</PropositionalEquality>

%<*Cong>
\begin{code}
cong : ∀ {a} {A B : Set a} {m n : A} → (f : A → B) → m ≡ n → f m ≡ f n
cong f refl = refl


id-0 : (n : ℕ) → (n + 0) ≡ (0 + n)
id-0 zero = refl
id-0 (suc n) = cong suc (id-0 n)
\end{code}
%</Cong>

%<*unfortunate>
\begin{code}
unfortunate : Set
unfortunate = ℕ → ℕ
\end{code}
%</unfortunate>

\begin{code}
Name = ℕ
\end{code}
%<*ActorRef>
\begin{code}
data ActorRef : (InboxType : Set) → Set where
    create-actor-ref : ∀ {A} → Name → ActorRef A
\end{code}
%</ActorRef>

%<*ExampleInbox>
\begin{code}
TestInbox : InboxShape
TestInbox = ( ReferenceType (
                    [ ValueType String ]ˡ
                    ∷ [ ValueType Bool ]ˡ
                    ∷ [])
                ∷ ValueType ℕ
                ∷ [])
            ∷ [ ValueType Bool ]ˡ
            ∷ []
\end{code}
%</ExampleInbox>

%<*Subtyping>
\begin{code}
Boolbox : InboxShape
Boolbox = [ [ ValueType Bool ]ˡ ]ˡ

test-subtyping : Boolbox <: TestInbox
test-subtyping = [ S Z ]ᵐ
\end{code}
%</Subtyping>
%<*PartialFunction>
\begin{code}
partial : (n : ℕ) → (n ≡ 2) → Bool
partial 0 ()
partial 1 ()
partial 2 p =  false
partial (suc (suc (suc n))) ()
\end{code}
%</PartialFunction>

%<*Identity>
\begin{code}
identity : (A : Set) → A → A
identity A v = v
\end{code}
%</Identity>

%<*ImplicitIdentity>
\begin{code}
identity' : {A : Set} → A → A
identity' v = v

f : ℕ → ℕ
f = identity'
g = identity' {Bool}
h = identity' 2
\end{code}
%</ImplicitIdentity>
