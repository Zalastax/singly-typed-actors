\begin{code}
module Examples.MonadParameterized where
open import Data.Bool using (Bool ; false ; true)
open import Data.Nat using (ℕ ; zero ; suc)
open import Data.Unit using (⊤ ; tt)
open import Data.String using (String)
open import Relation.Binary.PropositionalEquality using (_≡_ ; refl)

infixl 1 _>>=_
\end{code}

%<*Monad'>
\begin{code}
data MonadParam' (B : Set) : {p q : Set} → p → q → Set₁ where
    return : ∀ {p} → {pre : p} → B → MonadParam' B pre pre
    _>>=_ : ∀ {A X Y Z} → {pre : X} → {mid : Y} → {post : Z} →
            MonadParam' A pre mid →
            (A → MonadParam' B mid post) →
            MonadParam' B pre post
\end{code}
%</Monad'>

%<*Monad>
\begin{code}
data MonadParam (B : Set) : {p q : Set} → p → (B → q) → Set₁ where
    return : {S : Set} → {post : B → S} → (val : B) →
                MonadParam B (post val) post
    _>>=_ : ∀ {A X Y Z} → {pre : X} → {mid : A → Y} → {post : B → Z} →
                MonadParam A pre mid →
                ((x : A) → MonadParam B (mid x) post) →
                MonadParam B pre post
\end{code}
%</Monad>

%<*MonadFile>
\begin{minipage}{\textwidth}
\begin{code}

data FileState : Set where
  Open : String → FileState
  Closed : FileState
\end{code}
\end{minipage}

\begin{minipage}{\textwidth}
\begin{code}
data FileMonad : (A : Set) → FileState → (A → FileState) → Set₁ where
  return : ∀ {A post} → (val : A) → FileMonad A (post val) post
  _>>=_ : ∀ {A B pre mid post} →
            FileMonad A pre mid →
            ((x : A) → FileMonad B (mid x) post) →
            FileMonad B pre post
  OpenFile : String → FileMonad FileState Closed (λ x → x)
  CloseFile : ∀ { s } → FileMonad ⊤ (Open s) (λ _ → Closed)
  WriteFile : ∀ { s } → String → FileMonad ⊤ (Open s) (λ _ → Open s)
\end{code}
\end{minipage}

\begin{minipage}{\textwidth}
\begin{code}
writeHello : FileMonad ⊤ Closed (λ _ → Closed)
writeHello = OpenFile "world.txt" >>= λ {
  Closed → return _ ;
  (Open _) → WriteFile "hello" >>= λ _ → CloseFile
  }
\end{code}
\end{minipage}
%</MonadFile>

