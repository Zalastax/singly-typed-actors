\begin{code}
module Membership where
-- open import Data.List.Any.Membership.Propositional public

open import Data.List
open import Data.List.Any using ()
open import Data.List.All using (All ; [] ; _∷_)
open import Data.List.Properties using (++-assoc ; ++-identityʳ)
open import Relation.Binary.PropositionalEquality using (_≡_ ; refl)
open import Data.Empty using (⊥)
open import Level using (Level) renaming (zero to lzero ; suc to lsuc)
\end{code}
%<*ElementIn>
\begin{code}
data _∈_ {a} {A : Set a} : A → (List A) → Set a where
  Z : ∀ {x xs} → x ∈ (x ∷ xs)
  S : ∀ {x y xs} → x ∈ xs → x ∈ (y ∷ xs)
\end{code}
%</ElementIn>
\begin{code}
x∈[]-⊥ : ∀ {a} {A : Set a} {x : A} → x ∈ [] → ⊥
x∈[]-⊥ ()

\end{code}
%<*Subset>
\begin{code}
data _⊆_ {a} {A : Set a} : List A → List A → Set a where
  [] : ∀ {xs} → [] ⊆ xs
  _∷_ : ∀ {x xs ys} → x ∈ ys → xs ⊆ ys → (x ∷ xs) ⊆ ys
\end{code}
%</Subset>
\begin{code}


⊆-suc : ∀ {a} {A : Set a} {y : A} {xs ys : List A} → xs ⊆ ys → xs ⊆ (y ∷ ys)
⊆-suc [] = []
⊆-suc (x₁ ∷ subs) = (S x₁) ∷ (⊆-suc subs)

⊆-refl : ∀ {a} {A : Set a} {xs : List A} → xs ⊆ xs
⊆-refl {xs = []} = []
⊆-refl {xs = x ∷ xs} = Z ∷ (⊆-suc ⊆-refl)

-- TODO: discuss whether there's a way to not do this ugly trick while still having easy proofs
find-∈ : ∀ {a} {A : Set a} {ls : List A} {x : A} → (x ∈ ls) → A
find-∈ {x = x} _ = x
-- find-∈ {ls = x ∷ xs} Z = x
-- find-∈ (S px) = find-∈ px

\end{code}
%<*translate-sub>
\begin{code}
translate-⊆ : ∀ {a} {A : Set a} {ls ks : List A} {x : A} →
              (ls ⊆ ks) →
              (x ∈ ls) →
              (x ∈ ks)
translate-⊆ [] ()
translate-⊆ (x₂ ∷ subs) Z = x₂
translate-⊆ (x₂ ∷ subs) (S px) = translate-⊆ subs px
\end{code}
%</translate-sub>
\begin{code}

lookup-parallel : ∀ {a b} {A : Set a} {B : Set b} {x : A} {gss : List A} → x ∈ gss → (refs : List B) → (f : B → A) → map f refs ≡ gss → B
lookup-parallel Z [] f ()
lookup-parallel Z (x ∷ refs) f refl = x
lookup-parallel (S px) [] f ()
lookup-parallel (S px) (x ∷ refs) f refl = lookup-parallel px refs f refl

lookup-parallel-≡ : ∀ {a b} {A : Set a} {B : Set b} {x : A} {gss : List A} → (x₁ : x ∈ gss) → (refs : List B) → (f : B → A) → (eq : map f refs ≡ gss) → x ≡ f (lookup-parallel x₁ refs f eq)
lookup-parallel-≡ Z [] f ()
lookup-parallel-≡ Z (x ∷ refs) f refl = refl
lookup-parallel-≡ (S px) [] f ()
lookup-parallel-≡ (S px) (x ∷ refs) f refl = lookup-parallel-≡ px refs f refl

translate-∈ : ∀ {a b} {A : Set a} {B : Set b} {x : A} {gss : List A} (x₁ : x ∈ gss) → (refs : List B) → (f : B → A) → (eq : map f refs ≡ gss) → lookup-parallel x₁ refs f eq ∈ refs
translate-∈ Z [] f ()
translate-∈ (S px) [] f ()
translate-∈ Z (x ∷ refs) f refl = Z
translate-∈ (S px) (x ∷ refs) f refl = S (translate-∈ px refs f refl)

\end{code}
%<*lookup-all>
\begin{code}
lookup-all : ∀ {a p} {A : Set a} {P : A → Set p} {ls : List A} {x : A} →
              x ∈ ls →
              All P ls → P x
lookup-all Z (px ∷ pxs) = px
lookup-all (S px) (px₁ ∷ pxs) = lookup-all px pxs
\end{code}
%</lookup-all>
\begin{code}

All-⊆ : ∀ {a p} {A : Set a} {P : A → Set p} {xs ys} → xs ⊆ ys → All P ys → All P xs
All-⊆ {xs = []} {ys} subs pys = []
All-⊆ (x₁ ∷ subs) pys = lookup-all x₁ pys ∷ All-⊆ subs pys

⊆-trans : ∀ {a} {A : Set a} {as bs cs : List A} → as ⊆ bs → bs ⊆ cs → as ⊆ cs
⊆-trans [] _ = []
⊆-trans (x₁ ∷ asbs) bscs = (translate-⊆ bscs x₁) ∷ (⊆-trans asbs bscs)


⊆++ : {a : Level} {A : Set a}
      (xs ys zs : List A) →
      xs ⊆ zs → ys ⊆ zs →
      (xs ++ ys) ⊆ zs
⊆++ _ ys zs [] q = q
⊆++ _ ys zs (x₁ ∷ p) q = x₁ ∷ (⊆++ _ ys zs p q)

∈-insert : ∀ {a} {A : Set a}
           (xs ys : List A) →
           (x : A) →
           x ∈ (ys ++ x ∷ xs)
∈-insert xs [] x = Z
∈-insert xs (x₁ ∷ ys) x = S (∈-insert xs ys x)

-- count down both the proof and the list
-- Result depends on which is shortest
insert-one : ∀ {a} {A : Set a} {x₁ : A}
               (xs ys : List A) →
               (y : A) →
               x₁ ∈ (xs ++ ys) →
               x₁ ∈ (xs ++ y ∷ ys)
insert-one [] ys y p = S p
insert-one (x ∷ xs) ys y Z = Z
insert-one (x ∷ xs) ys y (S p) = S (insert-one xs ys y p)

⊆-insert : ∀ {a} {A : Set a}
                 (xs ys zs : List A) →
                 (x : A) →
                 xs ⊆ (ys ++ zs) →
                 xs ⊆ (ys ++ x ∷ zs)
⊆-insert [] ys zs x p = []
⊆-insert (x₁ ∷ xs) ys zs x (x₂ ∷ p) = insert-one ys zs x x₂ ∷
                                                 (⊆-insert xs ys zs x p)

⊆-move : {a : Level} {A : Set a}
                     (xs ys : List A) →
                     (xs ++ ys) ⊆ (ys ++ xs)
⊆-move [] ys rewrite (++-identityʳ ys) = ⊆-refl
⊆-move (x ∷ xs) ys with (⊆-move xs ys)
... | q = ∈-insert xs ys x ∷ ⊆-insert (xs ++ ys) ys xs x q

⊆-skip : {a : Level} {A : Set a}
         (xs ys zs : List A) →
         ys ⊆ zs →
         (xs ++ ys) ⊆ (xs ++ zs)
⊆-skip [] ys zs p = p
⊆-skip (x ∷ xs) ys zs p = Z ∷ (⊆-suc (⊆-skip xs ys zs p))

⊆++-refl : ∀ {a} {A : Set a} →
           (xs ys : List A) →
           xs ⊆ (xs ++ ys)
⊆++-refl [] ys = []
⊆++-refl (x ∷ xs) ys = Z ∷ (⊆-suc (⊆++-refl xs ys))

⊆++-l-refl : ∀ {a} {A : Set a} →
                   (xs ys : List A) →
                   xs ⊆ (ys ++ xs)
⊆++-l-refl xs [] = ⊆-refl
⊆++-l-refl xs (x ∷ ys) = ⊆-suc (⊆++-l-refl xs ys)

∈-inc : ∀ {a} {A : Set a}
              (xs ys : List A) →
              (x : A) →
              x ∈ xs →
              x ∈ (xs ++ ys)
∈-inc _ ys x Z = Z
∈-inc _ ys x (S p) = S (∈-inc _ ys x p)

⊆-inc : ∀ {a} {A : Set a}
              (xs ys zs : List A) →
              xs ⊆ ys →
              (xs ++ zs) ⊆ (ys ++ zs)
⊆-inc [] [] zs p = ⊆-refl
⊆-inc [] (x ∷ ys) zs p = ⊆++-l-refl zs (x ∷ ys)
⊆-inc (x ∷ xs) ys zs (px ∷ p) = ∈-inc ys zs x px ∷ (⊆-inc xs ys zs p)

⊆++comm : ∀ {a} {A : Set a}
            (xs ys zs : List A) →
            ((xs ++ ys) ++ zs) ⊆ (xs ++ (ys ++ zs))
⊆++comm [] ys zs = ⊆-refl
⊆++comm (x ∷ xs) ys zs = Z ∷ (⊆-suc (⊆++comm xs ys zs))

⊆++comm' : ∀ {a} {A : Set a}
           (xs ys zs : List A) →
           (xs ++ ys ++ zs) ⊆ ((xs ++ ys) ++ zs)
⊆++comm' [] ys zs = ⊆-refl
⊆++comm' (x ∷ xs) ys zs = Z ∷ ⊆-suc (⊆++comm' xs ys zs)

pad-pointer : ∀ {a} {A : Set a} → (l r : List A) → {v : A} → v ∈ r → v ∈ (l ++ r)
pad-pointer [] r p = p
pad-pointer (x ∷ l) r p = S (pad-pointer l r p)

record ElemInList {a} {A : Set a} (ls : List A) : Set a where
  field
    elem : A
    in-list : elem ∈ ls

tabulate-suc : ∀ {a} {A : Set a} → {ls : List A} → {x : A} → List (ElemInList ls) → List (ElemInList (x ∷ ls))
tabulate-suc [] = []
tabulate-suc (eil ∷ tabs) =
  let rec = tabulate-suc tabs
      open ElemInList
  in record { elem = eil .elem ; in-list = S (eil .in-list) } ∷ rec

tabulate-∈ : ∀ {a} {A : Set a} → (ls : List A) → List (ElemInList ls)
tabulate-∈ [] = []
tabulate-∈ (x ∷ ls) =
  let rec = tabulate-∈ ls
  in record { elem = x ; in-list = Z } ∷ tabulate-suc rec

\end{code}
