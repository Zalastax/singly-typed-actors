\begin{code}
module Selective.Libraries.ActiveObjects where

open import Selective.Libraries.Channel public
open import Prelude

open ChannelType
open ChannelInitiation

\end{code}
%<*ActiveMethod>
\begin{code}
data ActiveMethod : Set₁ where
  VoidMethod : InboxShape → ActiveMethod
  ResponseMethod : ChannelInitiation → ActiveMethod
\end{code}
%</ActiveMethod>
\begin{code}

active-request-type : ChannelInitiation → InboxShape
active-request-type ci = loop (ci .request-tagged)
  where
    open ChannelInitiation
    loop : ∀ {IS} → {request : InboxShape} → All (IsRequestMessage IS) request → InboxShape
    loop [] = []
    loop (px ∷ airm) =
      let rec = loop airm
      in extra-fields-shape px ∷ rec

\end{code}
%<*ResponseInput>
\begin{code}
record ResponseInput (ci : ChannelInitiation) : Set₁ where
  constructor _sent_
  field
    caller : InboxShape
    msg : Message (active-request-type ci)
\end{code}
%</ResponseInput>
\begin{code}

\end{code}
%<*state-vars>
\begin{code}
state-vars : ∀ {IS} {state-type : Set₁} →
              (state-type → TypingContext) →
              Message IS → state-type →
              TypingContext
state-vars vars input state = add-references (vars state) input
\end{code}
%</state-vars>
\begin{code}
reply-state-vars : ∀ {ci} {state-type : Set₁} →
                    (state-type → TypingContext) →
                    ResponseInput ci →
                    state-type →
                    TypingContext
reply-state-vars vars input state =
  let open ResponseInput
  in (input .caller) ∷ state-vars vars (input .msg) state

active-reply-needed-fields : ∀ mt → IsChannelMessage mt → MessageType
active-reply-needed-fields _ (HasTag MT) = MT

active-reply-type : ChannelType → InboxShape
active-reply-type ci = loop (ci .all-tagged)
  where
    open ChannelType
    loop : {channel-shape : InboxShape} → All IsChannelMessage channel-shape → InboxShape
    loop  [] = []
    loop (px ∷ aim) =
      let rec = loop aim
      in active-reply-needed-fields _ px ∷ rec

\end{code}
%<*ActiveReply>
\begin{code}
record ActiveReply (ci : ChannelInitiation)
                   (state-type : Set₁)
                   (vars : state-type → TypingContext)
                   (input : ResponseInput ci) : Set₁ where
  field
    new-state : state-type
    reply : SendMessage
              (active-reply-type (ci .response))
              (reply-state-vars vars input new-state)
\end{code}
%</ActiveReply>
\begin{code}

reply-vars : ∀ {st ci var-f input} → (st → TypingContext) → ResponseInput ci → ActiveReply ci st var-f input → TypingContext
reply-vars f input ar = reply-state-vars f input (ActiveReply.new-state ar)

active-method : InboxShape → (state-type : Set₁) → (state-type → TypingContext) → ActiveMethod → Set₂
active-method IS state-type var-f (VoidMethod IS') =
  let
\end{code}
%<*active-method-VoidMethod>
\begin{code}
    input-type = Message IS'

    return-type : input-type → Set₁
    return-type _ = state-type

    precondition : input-type → state-type → TypingContext
    precondition input state = state-vars var-f input state

    postcondition : (input : input-type) →
                    return-type input → TypingContext
    postcondition input state = state-vars var-f input state
\end{code}
%</active-method-VoidMethod>
\begin{code}
  in
    (i : Size) →
    (input : input-type) →
    (state : state-type) →
    ∞ActorM i IS
      (return-type input)
      (precondition input state)
      (postcondition input)
active-method IS state-type var-f (ResponseMethod ci) =
  let
    open ResponseInput
\end{code}
%<*active-method-ResponseMethod>
\begin{code}
    input-type = ResponseInput ci

    return-type : input-type → Set₁
    return-type input = ActiveReply ci state-type var-f input

    precondition : input-type → state-type → TypingContext
    precondition input state = reply-state-vars var-f input state

    postcondition : (input : input-type) →
                    return-type input → TypingContext
    postcondition input = reply-vars var-f input
\end{code}
%</active-method-ResponseMethod>
\begin{code}
  in
\end{code}
%<*method-handler>
\begin{code}
    (i : Size) →
    (input : input-type) →
    (state : state-type) →
    ∞ActorM i IS
      (return-type input)
      (precondition input state)
      (postcondition input)
\end{code}
%</method-handler>
\begin{code}

\end{code}
%<*methods-shape>
\begin{code}
active-method-request : ActiveMethod → InboxShape
active-method-request (VoidMethod x) = x
active-method-request (ResponseMethod x) = x .request

methods-shape : List ActiveMethod → InboxShape
methods-shape [] = []
methods-shape (am ∷ lci) =
  let rec = methods-shape lci
      am-shape = active-method-request am
  in am-shape ++ rec
\end{code}
%</methods-shape>
%<*ActiveObject>
\begin{code}
record ActiveObject : Set₂ where
  field
    state-type : Set₁
    vars : state-type → TypingContext
    methods : List ActiveMethod
    extra-messages : InboxShape
    handlers : All
                (active-method
                  (methods-shape methods ++ extra-messages)
                  state-type
                  vars
                )
                methods
\end{code}
%</ActiveObject>
\begin{code}

active-inbox-shape : ActiveObject → InboxShape
active-inbox-shape ac =
  let open ActiveObject
  in methods-shape (ac .methods) ++ (ac .extra-messages)

open ActiveObject

forget-message : ∀ {i Γ} →
                 {IS IS' : InboxShape} →
                 (m : Message IS) →
                 ∞ActorM i IS' ⊤₁ (add-references Γ m) (λ _ → Γ)
forget-message (Msg x x₁) = strengthen (⊆++-l-refl _ _)

data InEither {a} {A : Set a} (v : A) (xs ys : List A) : Set a where
  Left : (v ∈ xs) → InEither v xs ys
  Right : (v ∈ ys) → InEither v xs ys

∈++ : ∀ {a} {A : Set a} {v : A} → (xs ys : List A) → (v ∈ (xs ++ ys)) → InEither v xs ys
∈++ [] ys p = Right p
∈++ (x ∷ xs) ys Z = Left Z
∈++ (x ∷ xs) ys (S p) with (∈++ xs ys p)
... | Left q = Left (S q)
... | Right q = Right q

invoke-active-method : {i : Size} →
                       (ac : ActiveObject) →
                       (input : Message (methods-shape (ac .methods))) →
                       (state : ac .state-type) →
                       ∞ActorM i (active-inbox-shape ac)
                         (ac .state-type) (add-references (ac .vars state) input) (λ x → add-references (ac .vars x) input)
invoke-active-method ac (Msg x f) state = invoke f (ac .methods) (ac .handlers) x
  where
    translate-invoke-pointer : (ci : ChannelInitiation) →
                               {MT : MessageType} →
                               (ValueType UniqueTag ∷ ReferenceType (ci .response .channel-shape) ∷ MT) ∈ ci .request →
                               MT ∈ active-request-type ci
    translate-invoke-pointer record { request-tagged = HasTag+Ref MT ∷ _ } Z = Z
    translate-invoke-pointer record { response = response ; request-tagged = _ ∷ rt } (S p) =
      let rec = translate-invoke-pointer record { request = _ ; response = response ; request-tagged = rt } p
      in S rec
    translate-send-pointer : ∀ {ct MT} →
                               MT ∈ active-reply-type ct →
                               (TagField ∷ MT) ∈ ct .channel-shape
    translate-send-pointer {record { all-tagged = [] }} ()
    translate-send-pointer {record { all-tagged = HasTag MT ∷ _ }} Z = Z
    translate-send-pointer {record { channel-shape = x ∷ xs ; all-tagged = px ∷ pxs }} (S p) = S (translate-send-pointer p)

    invoke-void-method : {i : Size} {MT : MessageType} →
             All receive-field-content MT →
             (IS : InboxShape) →
             active-method (active-inbox-shape ac) (ac .state-type) (ac .vars) (VoidMethod IS) →
             MT ∈ IS →
             ∞ActorM i (active-inbox-shape ac) (ac .state-type) (extract-references MT ++ ac .vars state) (λ st → extract-references MT ++ ac .vars st)
    invoke-void-method f IS handler p =
      let
        active-message : Message IS
        active-message = Msg p f
      in do
        new-state ← handler _ active-message state
        return₁ new-state
    invoke-reply-method : {i : Size} {MT : MessageType} →
              All receive-field-content MT →
              (ci : ChannelInitiation) →
              active-method (active-inbox-shape ac) (ac .state-type) (ac .vars) (ResponseMethod ci) →
              MT ∈ (ci .request) →
              IsRequestMessage (ci .response .channel-shape) MT →
              ∞ActorM i (active-inbox-shape ac) (ac .state-type) (extract-references MT ++ ac .vars state) (λ st → extract-references MT ++ ac .vars st)
    invoke-reply-method (tag ∷ _ ∷ f) ci handler p (HasTag+Ref MT) =
      let
        active-message : Message (active-request-type ci)
        active-message = Msg (translate-invoke-pointer ci p) f
      in do
        record { new-state = new-state ; reply = SendM which fields } ← handler _ (_ sent active-message) state
        send Z (SendM (translate-send-pointer which) (lift tag ∷ fields))
        return₁ new-state
    invoke : {i : Size} {MT : MessageType} →
             All receive-field-content MT →
             (ams : List ActiveMethod) →
             All (active-method (active-inbox-shape ac) (ac .state-type) (ac .vars)) ams →
             MT ∈ methods-shape ams →
             ∞ActorM i (active-inbox-shape ac)
                       (ac .state-type)
                       (add-references-static (ac .vars state) MT)
                       (λ st → add-references-static (ac .vars st) MT)
    invoke _ _ [] ()
    invoke f (am ∷ _) (_ ∷ _) x with (∈++ (active-method-request am) _ x)
    invoke f (VoidMethod IS ∷ _) (handler ∷ _) x | Left q = invoke-void-method f IS handler q
    invoke f (ResponseMethod ci ∷ _) (handler ∷ _) x | Left q =
      let irm = lookup-all q (ci .request-tagged)
      in invoke-reply-method f ci handler q irm
    invoke f (_ ∷ cis) (_ ∷ handlers) x | Right q = invoke f cis handlers q


handle-active-method : {i : Size} →
                       (ac : ActiveObject) →
                       (input : Message (methods-shape (ac .methods))) →
                       (state : ac .state-type) →
                       ∞ActorM i (active-inbox-shape ac)
                         (ac .state-type) (add-references (ac .vars state) input) (ac .vars)
handle-active-method ac input state =
  do
    state' ← (invoke-active-method ac input state)
    forget-message input
    return₁ state'

accept-sublist-unwrapped : (xs ys zs : InboxShape) → ∀{MT} → MT ∈ (xs ++ ys ++ zs) → Bool
accept-sublist-unwrapped [] [] zs p = false
accept-sublist-unwrapped [] (y ∷ ys) zs Z = true
accept-sublist-unwrapped [] (y ∷ ys) zs (S p) = accept-sublist-unwrapped [] ys zs p
accept-sublist-unwrapped (x ∷ xs) ys zs Z = false
accept-sublist-unwrapped (x ∷ xs) ys zs (S p) = accept-sublist-unwrapped xs ys zs p


accept-sublist : (xs ys zs : InboxShape) → MessageFilter (xs ++ ys ++ zs)
accept-sublist xs ys zs (Msg received-message-type received-fields) = accept-sublist-unwrapped xs ys zs received-message-type

record AcceptSublistDependent (IS : InboxShape) (accepted-type : MessageType) : Set₁ where
  field
    accepted-which : accepted-type ∈ IS
    fields : All receive-field-content accepted-type

receive-active-method : {i : Size} →
                        {Γ : TypingContext} →
                        (ac : ActiveObject) →
                        ∞ActorM i (active-inbox-shape ac) (Message (methods-shape (ac .methods))) Γ (add-references Γ)
receive-active-method ac = do
    record { msg = Msg {MT} p f ; msg-ok = msg-ok } ← selective-receive (accept-sublist [] (methods-shape (ac .methods)) (ac .extra-messages))
    let record {accepted-which = aw ; fields = fields } = rewrap-message (methods-shape (ac .methods)) (ac .extra-messages) p f msg-ok
    return₁ (Msg {MT = MT} aw fields)
  where
    rewrap-message : ∀{MT} → (ys zs : InboxShape) → (p : MT ∈ (ys ++ zs)) → All receive-field-content MT → (accept-sublist-unwrapped [] ys zs p) ≡ true → AcceptSublistDependent ys MT
    rewrap-message [] zs which f ()
    rewrap-message (x ∷ ys) zs Z f p = record { accepted-which = Z ; fields = f }
    rewrap-message (x ∷ ys) zs (S which) f p =
      let
        rec = rewrap-message ys zs which f p
        open AcceptSublistDependent
      in record { accepted-which = S (rec .accepted-which) ; fields = rec .fields }

\end{code}
%<*run-active-object>
\begin{code}
run-active-object : {i : Size} →
                    (ac : ActiveObject) →
                    (state : ac .state-type) →
                    ∞ActorM (↑ i)
                            (active-inbox-shape ac)
                            (ac .state-type)
                            ((ac .vars) state)
                            (ac .vars)
run-active-object ac state .force =
  receive-active-method ac ∞>>= λ { m .force →
  handle-active-method ac m state ∞>>= λ state' →
  run-active-object ac state'
  }
\end{code}
%</run-active-object>
