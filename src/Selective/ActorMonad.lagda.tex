\begin{code}
module Selective.ActorMonad where
open import Prelude

mutual
  data MessageField : Set₁ where
    ValueType : Set → MessageField
    ReferenceType : InboxShape → MessageField

  MessageType = List MessageField

  InboxShape = List MessageType

_<:_ = _⊆_ {A = MessageType}

ReferenceTypes = List InboxShape
TypingContext = ReferenceTypes

_⊢_ : TypingContext → InboxShape → Set₁
Γ ⊢ T = T ∈ Γ

record _⊢>:_ (Γ : TypingContext) (requested : InboxShape) : Set₁ where
    constructor [_]>:_
    field
      {actual} : InboxShape
      actual-is-sendable : Γ ⊢ actual
      actual-handles-requested : requested <: actual

send-field-content : TypingContext → MessageField → Set₁
send-field-content Γ (ValueType A) = Lift (lsuc lzero) A
send-field-content Γ (ReferenceType requested) = Γ ⊢>: requested

record SendMessage (To : InboxShape) (Γ : TypingContext) : Set₁ where
  constructor SendM
  field
    {MT} : MessageType
    selected-message-type : MT ∈ To
    send-fields : All (send-field-content Γ) MT

receive-field-content : MessageField → Set
receive-field-content (ValueType A) = A
receive-field-content (ReferenceType Fw) = ⊤

record Message (To : InboxShape) : Set₁ where
  constructor Msg
  field
    {MT} : MessageType
    received-message-type : MT ∈ To
    received-fields : All receive-field-content MT

open Message

extract-references : MessageType → ReferenceTypes
extract-references [] = []
extract-references (ValueType x ∷ mt) = extract-references mt
extract-references (ReferenceType T ∷ mt) = T ∷ extract-references mt

add-references-static : TypingContext → MessageType → TypingContext
add-references-static Γ MT = extract-references MT ++ Γ

add-references : ∀ {To} → TypingContext → Message To → TypingContext
add-references Γ msg = add-references-static Γ (msg .MT)

\end{code}
%<*MessageFilter>
\begin{code}
MessageFilter : (IS : InboxShape) → Set₁
MessageFilter IS = Message IS → Bool
\end{code}
%</MessageFilter>

%<*SelectedMessage>
\begin{code}
record SelectedMessage {IS : InboxShape}
            (f : MessageFilter IS) : Set₁ where
  constructor sm:_[_]
  field
    msg : Message IS
    msg-ok : f msg ≡ true
\end{code}
%</SelectedMessage>

%<*SelectedMessageAddReferences>
\begin{code}
selected-type : ∀ {IS} {filter : MessageFilter IS} →
                SelectedMessage filter →
                MessageType
selected-type sm: msg [ msg-ok ] = msg .MT

add-selected-references : TypingContext → ∀ {IS}
                            {filter : MessageFilter IS} →
                            SelectedMessage filter →
                            TypingContext
add-selected-references Γ m =
    add-references-static Γ (selected-type m)
\end{code}
%</SelectedMessageAddReferences>
\begin{code}


⊤₁ : Set₁
⊤₁ = Lift (lsuc lzero) ⊤

infixl 1 _∞>>=_ _>>=_ _∞>>_ _>>_

data ActorM (i : Size) (IS : InboxShape) : (A : Set₁) →
                (pre : TypingContext) →
                (post : A → TypingContext) →
                Set₂

record ∞ActorM (i : Size) (IS : InboxShape) (A : Set₁)
                (pre : TypingContext)
                (post : A → TypingContext) :
                Set₂ where
    coinductive
    constructor delay_
    field force : ∀ {j : Size< i} → ActorM j IS A pre post

data ActorM (i : Size) (IS : InboxShape) where
    Return : ∀ {A post} → (val : A) → ActorM i IS A (post val) post
    _∞>>=_ : ∀ {A B pre mid post} → (m : ∞ActorM i IS A pre mid) →
        (f : (x : A) → ∞ActorM i IS B (mid x) (post)) →
        ActorM i IS B pre post
    Spawn : {NewIS : InboxShape} → {A : Set₁} → ∀ {pre postN} →
        ActorM i NewIS A [] postN →
        ActorM i IS ⊤₁ pre λ _ → NewIS ∷ pre
    Send : ∀ {pre} → {ToIS : InboxShape} →
        (canSendTo : pre ⊢ ToIS) →
        (msg : SendMessage ToIS pre) →
        ActorM i IS ⊤₁ pre (λ _ → pre)
    Receive : ∀ {pre} → ActorM i IS (Message IS) pre (add-references pre)
\end{code}
%<*SelectiveReceiveConstruct>
\AgdaTarget{SelectiveReceive}
\begin{code}
    SelectiveReceive : ∀ {pre} →
        (filter : MessageFilter IS) →
        ActorM i IS
          (SelectedMessage filter)
          pre
          (add-selected-references pre)
\end{code}
%</SelectiveReceiveConstruct>
\begin{code}
    Self : ∀ {pre} → ActorM i IS ⊤₁ pre (λ _ → IS ∷ pre)
    Strengthen   : ∀ {ys xs} → (inc : ys ⊆ xs) →
        ActorM i IS ⊤₁ xs (λ _ → ys)

--
-- ========== Helpers for ActorM ==========
--

open ∞ActorM public

-- coinduction helper for Value
return₁ : {A : Set (lsuc lzero)} → ∀ {i IS post} → (val : A) → ∞ActorM i IS A (post val) post
return₁ val .force = Return val

-- universe lifting for return₁
return : {A : Set} → ∀ {i IS post} → (val : A) → ∞ActorM i IS (Lift (lsuc lzero) A) (post (lift val)) post
return val = return₁ (lift val)

_>>=_ : ∀ {i IS A B pre mid post} → (m : ∞ActorM i IS A pre mid) →
    (f : (x : A) → ∞ActorM i IS B (mid x) (post)) →
    ∞ActorM i IS B pre post
(m >>= f) .force = m ∞>>= f

_∞>>_ : ∀ {i IS A B pre mid post} → (m : ∞ActorM i IS A pre (λ _ → mid)) →
    (n : ∞ActorM i IS B mid post) →
    ActorM i IS B pre post
m ∞>> n = m ∞>>= λ _ → n

[mid:_]_>>=_ : ∀ {i IS A B pre post} → ∀ mid →
    (m : ∞ActorM i IS A pre mid) →
    (f : (x : A) → ∞ActorM i IS B (mid x) (post)) →
    ∞ActorM i IS B pre post
[mid: mid ] m >>= f = _>>=_ {mid = mid} m f

_>>_ : ∀ {i IS A B pre mid post} → (m : ∞ActorM i IS A pre (λ _ → mid)) →
    (n : ∞ActorM i IS B mid post) →
    ∞ActorM i IS B pre post
(m >> n) .force = m ∞>> n

[mid:_]_>>_ : ∀ {i IS A B pre post} → ∀ mid →
    (m : ∞ActorM i IS A pre (λ _ → mid)) →
    (n : ∞ActorM i IS B mid (post)) →
    ∞ActorM i IS B pre post
[mid: mid ] m >> f = _>>_ {mid = mid} m f

-- coinduction helper for spawn
spawn : ∀ {i IS NewIS A pre postN} →
    ActorM i NewIS A [] postN →
    ∞ActorM (↑ i) IS ⊤₁ pre λ _ → NewIS ∷ pre
spawn newAct .force = Spawn newAct

-- spawn potentially infinite actors
spawn∞ : ∀ {i IS NewIS A pre postN} →
    ∞ActorM (↑ i) NewIS A [] postN →
    ∞ActorM (↑ i) IS ⊤₁ pre λ _ → NewIS ∷ pre
spawn∞ newAct = spawn (newAct .force)


send : ∀ {i IS ToIS pre} →
     (canSendTo : ToIS ∈ pre) →
     (SendMessage ToIS pre) →
     ∞ActorM (↑ i) IS ⊤₁ pre (λ _ → pre)
send p m .force = Send p m

-- coinduction helper and neater syntax for message sending
_![t:_]_ : ∀ {i IS ToIS pre MT} →
    (canSendTo : ToIS ∈ pre) →
    (MT ∈ ToIS) →
    All (send-field-content pre) MT →
    ∞ActorM (↑ i) IS ⊤₁ pre (λ _ → pre)
(canSendTo ![t: p ] fields) = send canSendTo (SendM p fields)

-- coinduction helper for Receive
receive : ∀ {i IS pre} → ∞ActorM (↑ i) IS (Message IS) pre (add-references pre)
receive .force = Receive

selective-receive : ∀ {i IS pre} → (filter : MessageFilter IS) →
  ∞ActorM (↑ i) IS (SelectedMessage filter) pre (add-selected-references pre)
selective-receive filter .force = SelectiveReceive filter

self : ∀ {i IS pre} → ∞ActorM (↑ i) IS ⊤₁ pre (λ _ → IS ∷ pre)
self .force = Self

-- coinduction helper for Strengthen
strengthen : ∀ {i IS xs ys} → ys ⊆ xs → ∞ActorM (↑ i) IS ⊤₁ xs (λ _ → ys)
strengthen inc .force = Strengthen inc

begin : ∀ {i IS A pre post} → ∞ActorM i IS A pre post → ActorM i IS A pre post
begin = _∞>>_ (return tt)

⊠-of-values : List Set → InboxShape
⊠-of-values [] = []
⊠-of-values (x ∷ vs) = ([ ValueType x ]ˡ) ∷ ⊠-of-values vs
\end{code}
