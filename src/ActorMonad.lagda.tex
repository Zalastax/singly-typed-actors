\begin{code}
module ActorMonad where
open import Prelude

\end{code}

An Actor is indexed by the shape of its inbox.

The shape dictates what types the messages sent to an actor can have,
 and thus also what types the messages being received by the actor can have.

The shape is constant over the actors whole life-time.
It would be nice to allow the shape to grow monotonically over time.

%<*InboxShape>
The typing rules for communication in \ourlang are based on the idea that a very important
property of a message is that it can be understood by the receiver.
The type system will thus be used to limit what type a message sent to an inbox can have.
By limiting the types of messages sent to an inbox,
we can make sure that every message read from the inbox has a valid type.
In terms of \textcite{DBLP:conf/agere/KosterCM16}, this is the interface of the actor.

\begin{minipage}{\textwidth}
\begin{code}
mutual
  data MessageField : Set₁ where
    ValueType : Set → MessageField
    ReferenceType : InboxShape → MessageField

  MessageType = List MessageField

  InboxShape = List MessageType
\end{code}
\end{minipage}

We define the type of an actor's inbox as an algebraic data-type with sums
(\AgdaFunction{InboxShape}) of products (\AgdaFunction{MessageType}).
The \AgdaFunction{InboxShape} is a variant type that details
all the messages that can be received from an inbox.
Every message sent to an actor will have exactly one of the types that is listed,
which we communicate as a tag attached to the message (see section~\ref{sec:messages}).
We can think of the \AgdaFunction{InboxShape} as a set of types,
and every message coming paired with a proof that the message is a type from that set.
To know what type a message has you have to inspect the proof,
and the fields of the message will become accessible.

\AgdaFunction{MessageType} also uses a list as its underlying data structure,
but for a rather different purpose.
\AgdaFunction{MessageType} is the type of a single message,
and every element in the list represents a single field in a record.
\AgdaFunction{MessageType} should thus be seen as a product type,
similar to Haskell's tuples.

The fields in \AgdaFunction{MessageType} are combinations of value types and reference types.
A value type is any type from Agda's lowest set universe.
Typical examples are \AgdaDatatype{Bool}, \AgdaDatatype{ℕ}, \AgdaDatatype{String}
\AgdaDatatype{List} \AgdaDatatype{ℕ},
\AgdaDatatype{Bool} \AgdaOperator{\AgdaFunction{×}} \AgdaDatatype{ℕ},
and many user-defined types that are non-polymorphic.
We limit the types to the lowest set universe as a sort of approximation of serializable values.
It would be possible to further constrain the types to only those that are serializable,
but due to its insignificance to the calculus we have opted not to.
A future improvement would be to ensure that all communication can be serialized,
which most importantly involves developing a serialization solution for $\Spawn$.

A reference provides the capability to send messages to an actor's inbox, where receiving a message
containing a reference is one of the few ways to increase an actor's capabilities.
The type of a reference specifies what type a message sent via the reference can have,
and is used to uphold the guarantee that every message in the receiver's inbox is well-typed.
By using typed actor references, the receiver does not need to worry about unexpected messages,
while senders can be sure that messages will be understood.
Typically, the reference type of a field should be the smallest set of types
that will be sent using that reference.

Below we have created an instance of an \AgdaFunction{InboxShape},
showcasing the important concepts.
\AgdaFunction{TestInbox} is an inbox that can receive two kinds of messages:
messages containing a single boolean value,
and messages containing a special kind of reference together with a natural number.
The reference in the second kind of message can be sent two kinds of messages as well:
messages containing a single boolean value,
and messages containing a single string value.

\AgdaCatchGroup{Examples/Types}{ExampleMessages}

\AgdaCatchGroup{Examples/Types}{TestInbox}
%</InboxShape>

%<*InboxShapeSubtype>
The Interface Segregation Principle (ISP) is a design principle that states
that no client should be forced to depend on methods it does not use;
when clients depend upon objects which contain methods used only by other clients,
this can lead to fragile code~\parencite{martin2002agile}.
In particular, if a client depends on an interface that contains
methods that the client does not use but others do,
that client will be affected by changes that those other clients force on the interface.
This idea has also been addressed in the context of actor systems
where \textcite{DBLP:conf/ecoop/HeWT14} uses the term \emph{type pollution problem}
to describe the issue of actor interfaces being too fat.

Just as the advice of ISP is to break each class into granular interfaces,
the advice of \textcite{DBLP:conf/ecoop/HeWT14} is
to break the type of inboxes into granular subtypes.
Subtyping of inboxes means that given two inboxes $A$ and $B$,
if $A <: B$, then every message in $A$ is also a valid message in $B$.
Since we model \AgdaFunction{InboxShape} as a set,
the subtype relation can be taken to just be the subset relation.

\begin{code}
_<:_ = _⊆_ {A = MessageType}
\end{code}

Our representation of subsets uses two data structures: \AgdaDatatype{∈} and \AgdaDatatype{⊆}.
\AgdaDatatype{∈} is the list or set membership relation,
which we model as a Peano number that tells at what position the element occurs in the list.
An element that appears at the head of a list is at index \AgdaInductiveConstructor{Z},
and the index of an element that appears somewhere in the tail of the list is
the successor (\AgdaInductiveConstructor{S}) of its index in the tail.

\AgdaCatchGroup{Membership}{ElementIn}

The subset relation \AgdaBound{A} \AgdaDatatype{⊆} \AgdaBound{B} holds if
every member of \AgdaBound{A} is also a member of \AgdaBound{B}.
This can be modelled as a function from indices of \AgdaBound{A} to indices of \AgdaBound{B},
but this turned out to be inconvenient for our purposes.
An alternative approach is to build subsets as a \emph{view} \parencite{DBLP:journals/jfp/McBrideM04, norell2008dependently}
of the lists in question.
A view is a data type that reveals something interesting about its indices, \eg that a list is a subset of another.
To define \AgdaDatatype{⊆} we state that the empty list (\AgdaInductiveConstructor{[]}) is a subset of all lists,
an if you are able to prove that an element is a member of a set,
we state that you can add (\AgdaInductiveConstructor{∷}) that element to a subset of the set.

\AgdaCatchGroup{Membership}{Subset}

The drawback of using subsets is that recursive subtypes are not captured,
but we deem it good enough for our purposes and simple to work with.
Proving that \mbox{\AgdaBound{A} \AgdaFunction{<:} \AgdaBound{B}} boils down to
providing an index into \AgdaBound{B} for every element in \AgdaBound{A}.
For example, \AgdaFunction{text{-}subtyping} proves that an inbox that only accepts
\AgdaFunction{BoolMessage} is a subtype of the \AgdaFunction{TestInbox} from before.

\AgdaCatchGroup{Examples/Types}{TestInbox}

\AgdaCatchGroup{Examples/Types}{Subtyping}
%</InboxShapeSubtype>

%<*ActorPrecondition>
A reference to an actor is a name that is used to look up that actor's inbox in order to write or read from it.
We assign types to references in order to statically guarantee that we will only send messages to an inbox which the receiving actor can understand.
The property to maintain for this to hold is that the type of the reference must be a subtype of the inbox it references.
That is, given that an inbox is globally referred to via $\mathit{name}$ and has messages of type $T$,
the type of a reference to $\mathit{name}$ must have a type $S$, such that $S <: T$:

\input{include/figures/reference_to_inbox}

The desire to statically check that references are well-typed has affected the design of \ourlang a lot.
We first tried an encoding of references where a name is simply tagged with a type:
\AgdaCatch{Examples/Types}{ActorRef}
This encoding is intuitive and simple, but it does not capture $S <: T$, nor does it capture $name \mapsto inbox$.
Using this encoding thus requires finding a way to maintain additional proofs.

To maintain the necessary proofs of references being valid requires that we encode more things in the type system.
The solution that seems to fit best in the setting of Agda is to make a bigger distinction between references and values.
Values are kept as normal Agda terms, only restricted under Agda's usual typing rules.
References, on the other hand, are treated as variables that are maintained explicitly in the model,
encoded in the type parameter of each actor.
This technique makes it possible to maintain type correctness of references,
as long as the effect every actor operation has on the reference variables is carefully designed.

We type check references in the standard way of maintaining a variable typing context.
A typing context associates variables to types,
where variables are commonly referred to by their name.
Variable names make expressions easy to understand for humans,
but pose two annoying problems: α-equivalence and α-renaming.

α-equivalence is a form of equivalence that captures the intuition that the particular choice of
a bound variable name does not usually matter \parencite{turbak2008design}.
Renaming the variables of an expression in a way that preserves α-equivalence
is called α-renaming \parencite{turbak2008design}.
α-renaming is a part of the general concept of substitution,
the operation of replacing free occurrences of a variable with an expression.
The role of α-renaming in substitution is to avoid accidental variable name captures by renaming
variables so that substitution does not change the meaning of functions \parencite{turbak2008design}.

In order to avoid the problem of α-equivalence and α-renaming,
a common formalization technique is the use of de Bruijn indices
to represent variable binders \parencite{DBLP:journals/entcs/BerghoferU07}.
A de Bruijn index is a natural number that represents an occurrence of a variable in a λ-term,
and denotes the number of binders that are in scope between that occurrence
and its corresponding binder.
Table~\ref{tab:debruijn} shows some examples comparing a λ-calculus with names
to a λ-calculus using de Bruijn indices.

\input{include/figures/debruijn}

What makes de Bruijn indices easy to work with in Agda is that it lets us manage
the variable typing context as a list of types, with variables as
(de Bruijn) indices into that list.
We choose to represent the indices as the membership proposition
in order to make the de Bruijn indices correct by construction.
This lets us define the type judgement $Γ ⊢ T$ as:

\begin{minipage}{\textwidth}
\begin{code}
ReferenceTypes = List InboxShape
TypingContext = ReferenceTypes

_⊢_ : TypingContext → InboxShape → Set₁
Γ ⊢ T = T ∈ Γ
\end{code}
\end{minipage}
%</ActorPrecondition>

%<*ReferenceSubtype>
For inboxes, the subtype relation $A <: B$ says that
every message in $A$ is also a valid message in $B$.
It should therefore be possible to downcast a reference of type $B$ to a reference of type $A$,
since every message sent to a reference of type $A$ will be a valid message in $B$.
We can see that the subtype relation for references is reversed,
and is thus contravariant in its argument \parencite{DBLP:conf/ecoop/HeWT14}.
We capture this property in a special version of the reference typing judgement,
where $Γ ⊢>: T$ says that T is a subtype of some type in Γ.

\begin{minipage}{\textwidth}
\begin{code}
record _⊢>:_ (Γ : TypingContext)
             (requested : InboxShape) : Set₁ where
  constructor [_]>:_
  field
    {actual} : InboxShape
    actual-is-sendable : Γ ⊢ actual
    actual-handles-requested : requested <: actual

\end{code}
\end{minipage}

The notion of a subtype for references is important to implement
the pattern of sending a command together with what reference to reply to,
since different actors receiving the reply will have a different \AgdaFunction{InboxShape}.
This pattern, together with a selective receive construct,
can be used to implement synchronous calls,
which we explore in chapter~\ref{chap:selective_receive}.
%</ReferenceSubtype>

%<*Messages>
A message in \ourlang is made up of a tag, indicating the type of the message,
and instantiations of the fields in that message type.
We have made the unusual decision to give outgoing and incoming messages slightly different shapes.
This choice is purely for ease of implementation and does not affect the power of the model.

An outgoing message is made of two parts: the tag that indicates which type of message it is
and instantiations of every field of the type selected by the tag.
We index the type of a message by the type of inbox it is being sent to and incidentally by the
variable typing context.
Selecting which type variant this message has is done by indexing
into the \AgdaFunction{InboxShape}, using the $∈$ property.
The rest of the message is made up of instantiations of the fields
from the selected \AgdaFunction{MessageType}.
For values, the instantiation of a field is simply an Agda term of the type specified by the field.
For references, the instantiation is not a simple Agda term,
but rather a variable in the reference context.
The type of the selected reference variable must be compatible with the type that the receiver expects,
\ie they must have the correct subtype relation.

\begin{minipage}{\textwidth}
\begin{code}
send-field-content : TypingContext → MessageField → Set₁
send-field-content Γ (ValueType A) = Lift (lsuc lzero) A
send-field-content Γ (ReferenceType requested) = Γ ⊢>: requested
\end{code}
\end{minipage}

\begin{minipage}{\textwidth}
\begin{code}
record SendMessage (To : InboxShape)
                   (Γ : TypingContext) : Set₁ where
  constructor SendM
  field
    {MT} : MessageType
    selected-message-type : MT ∈ To
    send-fields : All (send-field-content Γ) MT
\end{code}
\end{minipage}

Incoming messages differ from outgoing messages in the instantiation of reference fields.
Looking at \AgdaFunction{receive{-}field{-}content} below,
one would expect the content of a reference field to be some representation of a reference,
\eg an index into the variable context.
Instead we find the unit type \AgdaDatatype{⊤}, which has no computational content at all.
The answer to this puzzle lies in that receiving a message will have the side-effect of
adding the references from every reference field to the variable context.
We saw in figure~\ref{fig:actorm} that the shape of the variable typing context is maintained
in the type of the monad,
making it easy to create indices into the variable context
without making them a part of the message.
It would be possible to make the reference field content be an index into the variable context,
but it would make the model slightly more complicated without making it more powerful;
the programmer can already create indices when they are needed and, since the typing context
is constantly changing, a specific index is often not valid for long.  

\begin{minipage}{\textwidth}
\begin{code}
receive-field-content : MessageField → Set
receive-field-content (ValueType A) = A
receive-field-content (ReferenceType Fw) = ⊤
\end{code}
\end{minipage}

\begin{minipage}{\textwidth}
\begin{code}
record Message (To : InboxShape) : Set₁ where
  constructor Msg
  field
    {MT} : MessageType
    received-message-type : MT ∈ To
    received-fields : All receive-field-content MT
\end{code}
\end{minipage}

\begin{minipage}{\textwidth}
\begin{code}
extract-references : MessageType → ReferenceTypes
extract-references [] = []
extract-references (ValueType x ∷ mt) = extract-references mt
extract-references (ReferenceType T ∷ mt) = T ∷ extract-references mt
\end{code}
\end{minipage}

\begin{minipage}{\textwidth}
\begin{code}
add-references : ∀ {To} → TypingContext → Message To → TypingContext
add-references Γ (Msg {MT} x x₁) = extract-references MT ++ Γ
\end{code}
\end{minipage}
%</Messages>
\begin{code}
infixl 1 _∞>>=_ _>>=_ _∞>>_ _>>_
⊤₁ : Set₁
⊤₁ = Lift (lsuc lzero) ⊤
\end{code}
%<*ActorMonadHeader>
\begin{code}
data ActorM (i : Size) (IS : InboxShape) : (A : Set₁) →
              (pre : TypingContext) →
              (post : A → TypingContext) →
              Set₂

record ∞ActorM (i : Size) (IS : InboxShape) (A : Set₁)
               (pre : TypingContext)
               (post : A → TypingContext) :
               Set₂ where
  coinductive
  constructor delay_
  field force : ∀ {j : Size< i} → ActorM j IS A pre post

data ActorM (i : Size) (IS : InboxShape) where
\end{code}
%</ActorMonadHeader>
%<*ActorMonadBindReturn>
\begin{code}
  Return : ∀ {A post} →
    (val : A) →
    ActorM i IS A (post val) post

  _∞>>=_ : ∀ {A B pre mid post} →
    (m : ∞ActorM i IS A pre mid) →
    (f : (x : A) → ∞ActorM i IS B (mid x) (post)) →
    ActorM i IS B pre post
\end{code}
%</ActorMonadBindReturn>
%<*ActorMonadSpawn>
\begin{code}
  Spawn : {NewIS : InboxShape} → {A : Set₁} → ∀ {pre postN} →
    ActorM i NewIS A [] postN →
    ActorM i IS ⊤₁ pre λ _ → NewIS ∷ pre
\end{code}
%</ActorMonadSpawn>
%<*ActorMonadSend>
\begin{code}
  Send : ∀ {pre} → {ToIS : InboxShape} →
    (canSendTo : pre ⊢ ToIS) →
    (msg : SendMessage ToIS pre) →
    ActorM i IS ⊤₁ pre (λ _ → pre)
\end{code}
%</ActorMonadSend>
%<*ActorMonadReceive>
\begin{code}
  Receive : ∀ {pre} →
    ActorM i IS (Message IS) pre (add-references pre)
\end{code}
%</ActorMonadReceive>
%<*ActorMonadSelf>
\begin{code}
  Self : ∀ {pre} →
    ActorM i IS ⊤₁ pre (λ _ → IS ∷ pre)
\end{code}
%</ActorMonadSelf>
%<*ActorMonadStrengthen>
\begin{code}
  Strengthen   : ∀ {ys xs} →
    (inc : ys ⊆ xs) →
    ActorM i IS ⊤₁ xs (λ _ → ys)
\end{code}
%</ActorMonadStrengthen>
%<*ActorMonadHelpers>
\begin{code}

--
-- ========== Helpers for ActorM ==========
--

open ∞ActorM public

-- coinduction helper for Value
return₁ : {A : Set (lsuc lzero)} → ∀ {i IS post} → (val : A) →
          ∞ActorM i IS A (post val) post
return₁ val .force = Return val

-- universe lifting for return₁
return : {A : Set} → ∀ {i IS post} → (val : A) →
         ∞ActorM i IS (Lift (lsuc lzero) A) (post (lift val)) post
return val = return₁ (lift val)

_>>=_ : ∀ {i IS A B pre mid post} → (m : ∞ActorM i IS A pre mid) →
  (f : (x : A) → ∞ActorM i IS B (mid x) (post)) →
  ∞ActorM i IS B pre post
(m >>= f) .force = m ∞>>= f

_∞>>_ : ∀ {i IS A B pre mid post} → (m : ∞ActorM i IS A pre (λ _ → mid)) →
  (n : ∞ActorM i IS B mid post) →
  ActorM i IS B pre post
m ∞>> n = m ∞>>= λ _ → n

[mid:_]_>>=_ : ∀ {i IS A B pre post} → ∀ mid →
  (m : ∞ActorM i IS A pre mid) →
  (f : (x : A) → ∞ActorM i IS B (mid x) (post)) →
  ∞ActorM i IS B pre post
[mid: mid ] m >>= f = _>>=_ {mid = mid} m f

_>>_ : ∀ {i IS A B pre mid post} → (m : ∞ActorM i IS A pre (λ _ → mid)) →
  (n : ∞ActorM i IS B mid post) →
  ∞ActorM i IS B pre post
(m >> n) .force = m ∞>> n

[mid:_]_>>_ : ∀ {i IS A B pre post} → ∀ mid →
  (m : ∞ActorM i IS A pre (λ _ → mid)) →
  (n : ∞ActorM i IS B mid (post)) →
  ∞ActorM i IS B pre post
[mid: mid ] m >> f = _>>_ {mid = mid} m f

-- coinduction helper for spawn
spawn : ∀ {i IS NewIS A pre postN} →
  ActorM i NewIS A [] postN →
  ∞ActorM (↑ i) IS ⊤₁ pre λ _ → NewIS ∷ pre
spawn newAct .force = Spawn newAct

-- spawn potentially infinite actors
spawn∞ : ∀ {i IS NewIS A pre postN} →
  ∞ActorM (↑ i) NewIS A [] postN →
  ∞ActorM (↑ i) IS ⊤₁ pre λ _ → NewIS ∷ pre
spawn∞ newAct = spawn (newAct .force)

-- coinduction helper and neater syntax for message sending
_![t:_]_ : ∀ {i IS ToIS pre MT} →
  (canSendTo : ToIS ∈ pre) →
  (MT ∈ ToIS) →
  All (send-field-content pre) MT →
  ∞ActorM (↑ i) IS ⊤₁ pre (λ _ → pre)
(canSendTo ![t: p ] fields) .force = Send canSendTo (SendM p fields)

-- coinduction helper for Receive
receive : ∀ {i IS pre} → ∞ActorM (↑ i) IS (Message IS) pre (add-references pre)
receive .force = Receive

self : ∀ {i IS pre} → ∞ActorM (↑ i) IS ⊤₁ pre (λ _ → IS ∷ pre)
self .force = Self

-- coinduction helper for Strengthen
strengthen : ∀ {i IS xs ys} → ys ⊆ xs → ∞ActorM (↑ i) IS ⊤₁ xs (λ _ → ys)
strengthen inc .force = Strengthen inc

begin : ∀ {i IS A pre post} → ∞ActorM i IS A pre post → ActorM i IS A pre post
begin = _∞>>_ (return tt)

⊠-of-values : List Set → InboxShape
⊠-of-values [] = []
⊠-of-values (x ∷ vs) = ([ ValueType x ]ˡ) ∷ ⊠-of-values vs
\end{code}
%</ActorMonadHelpers>
